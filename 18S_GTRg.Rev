################################################################################
#
# GTR + Gamma, ADS
# MArch 30, 2016
#
################################################################################

#######################
# Reading in the Data #
#######################

###### This just defines a single model for all sites #######

### Read in sequence data for both genes
www = getwd()

setwd(www)

data <- readDiscreteCharacterData("host18SMSA1Export.nex")

# Get some useful variables from the data. We need these later on.
n_species <- data.ntaxa()
taxa <- data.taxa()
n_branches <- 2 * n_species - 3

# set my move index
mi = 0



######################
# Substitution Model #
######################

#### specify the GTR+G substitution model applied uniformly to all sites ###
er_prior <- v(1,1,1,1,1,1)
er ~ dnDirichlet(er_prior)
moves[++mi] = mvSimplexElementScale(er,weight=3)

pi_prior <- v(1,1,1,1) 
pi ~ dnDirichlet(pi_prior)
moves[++mi] = mvSimplexElementScale(pi,weight=2)

#### create a deterministic variable for the rate matrix ####
Q := fnGTR(er,pi) 

#### monitor difference between empirical and estimated base frequencies ####
pi_empirical <- data.getEmpiricalBaseFrequencies()
# pi_diff := pi_empirical - pi

#############################
# Among Site Rate Variation #
#############################

alpha_prior <- 0.05
alpha ~ dnExponential( alpha_prior )
gamma_rates := fnDiscretizeGamma(alpha, alpha, 4, false)

# add moves the shape parameter
moves[++mi] = mvScale(alpha, weight=2)


##############
# Tree model #
##############

# construct a variable for the tree drawn from a birth death process
topology ~ dnUniformTopology(taxa)

# add topology Metropolis-Hastings moves
moves[++mi] = mvNNI(topology, weight=1.0)
moves[++mi] = mvSPR(topology, weight=1.0)

# create branch length vector and add moves
for (i in 1:n_branches) {
   br_lens[i] ~ dnExponential(10.0)
   moves[++mi] = mvScale(br_lens[i])
}

# add deterministic node to monitor tree length
TL := sum(br_lens)

# unite topology and branch length vector into phylogeny object
phylogeny := treeAssembly(topology, br_lens)



###################
# PhyloCTMC Model #
###################

# the sequence evolution model
seq ~ dnPhyloCTMC(tree=phylogeny, Q=Q, type="DNA")

# attach the data
seq.clamp(data)



#############
# THE Model #
#############

# We define our model.
# We can use any node of our model as a handle, here we chose to use the rate matrix.
mymodel = model(Q)


monitors[1] = mnModel(filename="output/host18S_bayesian1.log",printgen=10, separator = TAB)
monitors[2] = mnFile(filename="output/host18S_bayesian1.trees",printgen=10, separator = TAB, phylogeny)
monitors[3] = mnScreen(printgen=1000, TL)

mymcmc = mcmc(model=mymodel, monitors=monitors, moves=moves, nruns=2)
###Notice that nruns = 2 is the reason why the output produces 2 trees.
###Normally you want to do 4. This is expensive tho

mymcmc.burnin(generations=10000,tuningInterval=1000)
mymcmc.run(generations=35000)



# Now, we will analyze the tree output.
# Let us start by reading in the tree trace
treetrace1 = readTreeTrace("host18S_bayesian1_run1.trees", treetype="non-clock")
treetrace2 = readTreeTrace("host18S_bayesian1_run2.trees", treetype="non-clock")
# and get the summary of the tree trace
#treetrace.summarize()

map_tree = mapTree(treetrace1,"host18S_bayesian1.tree")
map_tree = mapTree(treetrace2,"host18S_bayesian1.tree")

# you may want to quit RevBayes now
# q()

	